/**
 * Returns location.  Returns null if no location was found.
 *
 *! Variables:
 *? location
 * * location where the teleport begins.
 *? target
 *  * target that will teleport.
 *? distance
 *  * distance to go.
 *? density
 *  * density checking 0/1(TRUE/FALSE).
 *? errorx
 *  * Random error in tile placement x.
 *? errory
 *  * Random error in tile placement y.
 *? eoffsetx
 *  * Random offset in tile placement x.
 *? eoffsety
 *  * Random offset in tile placement y.
 *
 *! Long-form explaination:
 * Location where the teleport begins, target that will teleport, distance to go, density checking 0/1(yes/no).
 * Random error in tile placement x, error in tile placement y, and block offset.
 * Block offset tells the proc how to place the box. Behind teleport location, relative to starting location, forward, etc.
 * Negative values for offset are accepted, think of it in relation to North, -x is west, -y is south. Error defaults to positive.
 * Turf and target are seperate in case you want to teleport some distance from a turf the target is not standing on or something.
 */
/proc/get_teleport_loc(turf/location, mob/target, distance = 1, density_check = FALSE,  closed_turf_check = FALSE, errorx = 0, errory = 0, eoffsetx = 0, eoffsety = 0)
	// Generic location finding variable.
	var/dirx = 0
	var/diry = 0

	// Generic counter for offset location.
	var/xoffset = 0
	var/yoffset = 0

	//Generic placing for point A in box. The lower left.
	var/b1xerror = 0
	var/b1yerror = 0

	//Generic placing for point B in box. The upper right.
	var/b2xerror = 0
	var/b2yerror = 0

	//Error should never be negative.
	errorx = abs(errorx)
	errory = abs(errory)

	// This can be done through equations but switch is the simpler method. And works fast to boot.
	switch(target.dir)
	// Directs on what values need modifying.
		if(NORTH) //North
			diry += distance
			yoffset += eoffsety
			xoffset += eoffsetx
			b1xerror -= errorx
			b1yerror -= errory
			b2xerror += errorx
			b2yerror += errory
		if(SOUTH) //South
			diry -= distance
			yoffset -= eoffsety
			xoffset += eoffsetx
			b1xerror -= errorx
			b1yerror -= errory
			b2xerror += errorx
			b2yerror += errory
		if(EAST) //East
			dirx += distance
			yoffset += eoffsetx//Flipped.
			xoffset += eoffsety
			b1xerror -= errory//Flipped.
			b1yerror -= errorx
			b2xerror += errory
			b2yerror += errorx
		if(WEST)//West
			dirx -= distance
			yoffset -= eoffsetx//Flipped.
			xoffset += eoffsety
			b1xerror -= errory//Flipped.
			b1yerror -= errorx
			b2xerror += errory
			b2yerror += errorx

	var/turf/destination = locate(location.x + dirx, location.y + diry, location.z)

	if(!destination)//If there isn't a destination.
		return


	if(!errorx && !errory)//If errorx or y were not specified.
		if(density_check && destination.density)
			return
		if(closed_turf_check && iswall(destination))
			return//If closed was specified.
		if(destination.x>world.maxx || destination.x<1)
			return
		if(destination.y>world.maxy || destination.y<1)
			return

	// To add turfs to list.
	var/list/destination_list = list()
	//destination_list = new()
	/**
	 * This will draw a block around the target turf, given what the error is.
	 * Specifying the values above will basically draw a different sort of block.
	 * If the values are the same, it will be a square. If they are different, it will be a rectengle.
	 * In either case, it will center based on offset. Offset is position from center.
	 * Offset always calculates in relation to direction faced. In other words, depending on the direction of the teleport,
	 * the offset should remain positioned in relation to destination.
	 */

	var/turf/center = locate((destination.x + xoffset), (destination.y + yoffset), location.z)//So now, find the new center.

	// Now to find a box from center location and make that our destination.
	var/width = (b2xerror - b1xerror) + 1
	var/height = (b2yerror - b1yerror) + 1
	for(var/turf/current_turf as anything in CORNER_BLOCK_OFFSET(center, width, height, b1xerror, b1yerror))
		if(density_check && current_turf.density)
			continue // If density was specified.
		if(closed_turf_check && iswall(current_turf))
			continue // If closed was specified.
		if(current_turf.x > world.maxx || current_turf.x < 1)
			continue // Don't want them to teleport off the map.
		if(current_turf.y > world.maxy || current_turf.y < 1)
			continue
		destination_list += current_turf

	if(!destination_list.len)
		return

	destination = pick(destination_list)
	return destination

/**
 * Returns the top-most atom sitting on the turf.
 * For example, using this on a disk, which is in a bag, on a mob,
 * will return the mob because it's on the turf.
 *
 * Arguments
 * * something_in_turf - a movable within the turf, somewhere.
 * * stop_type - stops looking if stop_type is found in the turf, returning that type (if found).
 * * return_any - if set to TRUE, will return last movable found even if its not of the passed type
 **/
/proc/get_atom_on_turf(atom/movable/something_in_turf, stop_type = null, return_any = FALSE)
	if(!istype(something_in_turf))
		CRASH("get_atom_on_turf was not passed an /atom/movable! Got [isnull(something_in_turf) ? "null":"type: [something_in_turf.type]"]")

	var/atom/movable/topmost_thing = something_in_turf

	while(topmost_thing?.loc && !isturf(topmost_thing.loc))
		topmost_thing = topmost_thing.loc
		if(stop_type && istype(topmost_thing, stop_type))
			return topmost_thing

	if (!stop_type || return_any)
		return topmost_thing

///Returns the turf located at the map edge in the specified direction relative to target_atom used for mass driver
/proc/get_edge_target_turf(atom/target_atom, direction)
	var/turf/target = locate(target_atom.x, target_atom.y, target_atom.z)
	if(!target_atom || !target)
		return 0
		//since NORTHEAST == NORTH|EAST, etc, doing it this way allows for diagonal mass drivers in the future
		//and isn't really any more complicated

	var/x = target_atom.x
	var/y = target_atom.y
	if(direction & NORTH)
		y = world.maxy
	else if(direction & SOUTH) //you should not have both NORTH and SOUTH in the provided direction
		y = 1
	if(direction & EAST)
		x = world.maxx
	else if(direction & WEST)
		x = 1
	if(ISDIAGONALDIR(direction)) //let's make sure it's accurately-placed for diagonals
		var/lowest_distance_to_map_edge = min(abs(x - target_atom.x), abs(y - target_atom.y))
		return get_ranged_target_turf(target_atom, direction, lowest_distance_to_map_edge)
	return locate(x,y,target_atom.z)

/**
 * returns turf relative to target_atom in given direction at set range
 * result is bounded to map size
 * note range is non-pythagorean
 */
/proc/get_ranged_target_turf(atom/target_atom, direction, range)
	var/x = target_atom.x
	var/y = target_atom.y
	if(direction & NORTH)
		y = min(world.maxy, y + range)
	else if(direction & SOUTH)
		y = max(1, y - range)
	if(direction & EAST)
		x = min(world.maxx, x + range)
	else if(direction & WEST) //if you have both EAST and WEST in the provided direction, then you're gonna have issues
		x = max(1, x - range)

	return locate(x,y,target_atom.z)

/// returns turf relative to target_atom offset in dx and dy tiles, bound to map limits
/proc/get_offset_target_turf(atom/target_atom, dx, dy)
	var/x = min(world.maxx, max(1, target_atom.x + dx))
	var/y = min(world.maxy, max(1, target_atom.y + dy))
	return locate(x, y, target_atom.z)

/**
 * Lets the turf this atom's *ICON* appears to inhabit
 * it takes into account:
 * Pixel_x/y
 * Matrix x/y
 * NOTE: if your atom has non-standard bounds then this proc
 * will handle it, but:
 * if the bounds are even, then there are an even amount of "middle" turfs, the one to the EAST, NORTH, or BOTH is picked
 * this may seem bad, but you're at least as close to the center of the atom as possible, better than byond's default loc being all the way off)
 * if the bounds are odd, the true middle turf of the atom is returned
**/
/proc/get_turf_pixel(atom/checked_atom)
	var/turf/atom_turf = get_turf(checked_atom) //use checked_atom's turfs, as its coords are the same as checked_atom's AND checked_atom's coords are lost if it is inside another atom
	if(!atom_turf)
		return null

	var/list/offsets = get_visual_offset(checked_atom)
	return pixel_offset_turf(atom_turf, offsets)

/**
 * Returns how visually "off" the atom is from its source turf as a list of x, y (in pixel steps)
 * it takes into account:
 * Pixel_x/y
 * Matrix x/y
 * Icon width/height
**/
/proc/get_visual_offset(atom/checked_atom)
	//Find checked_atom's matrix so we can use its X/Y pixel shifts
	var/matrix/atom_matrix = matrix(checked_atom.transform)

	var/pixel_x_offset = checked_atom.pixel_x + checked_atom.pixel_w + atom_matrix.get_x_shift()
	var/pixel_y_offset = checked_atom.pixel_y + checked_atom.pixel_z + atom_matrix.get_y_shift()

	//Irregular objects
	var/list/icon_dimensions = get_icon_dimensions(checked_atom.icon)
	var/checked_atom_icon_height = icon_dimensions["height"]
	var/checked_atom_icon_width = icon_dimensions["width"]
	if(checked_atom_icon_height != world.icon_size || checked_atom_icon_width != world.icon_size)
		pixel_x_offset += ((checked_atom_icon_width / world.icon_size) - 1) * (world.icon_size * 0.5)
		pixel_y_offset += ((checked_atom_icon_height / world.icon_size) - 1) * (world.icon_size * 0.5)

	return list(pixel_x_offset, pixel_y_offset)

/**
 * Takes a turf, and a list of x and y pixel offsets and returns the turf that the offset position best lands in
**/
/proc/pixel_offset_turf(turf/offset_from, list/offsets)
	//DY and DX
	var/rough_x = round(round(offsets[1], world.icon_size) / world.icon_size)
	var/rough_y = round(round(offsets[2], world.icon_size) / world.icon_size)

	var/final_x = clamp(offset_from.x + rough_x, 1, world.maxx)
	var/final_y = clamp(offset_from.y + rough_y, 1, world.maxy)

	if(final_x || final_y)
		return locate(final_x, final_y, offset_from.z)
	return offset_from

/// Similar function to RANGE_TURFS(), but will search spiralling outwards from the center (like the above, but only turfs).
/proc/spiral_range_turfs(dist = 0, center = usr, orange = FALSE, list/outlist = list(), tick_checked)
	outlist.Cut()
	if(!dist)
		outlist += center
		return outlist

	var/turf/t_center = get_turf(center)
	if(!t_center)
		return outlist

	var/list/turf_list = outlist
	var/turf/checked_turf
	var/y
	var/x
	var/c_dist = 1

	if(!orange)
		turf_list += t_center

	while( c_dist <= dist )
		y = t_center.y + c_dist
		x = t_center.x - c_dist + 1
		for(x in x to t_center.x + c_dist)
			checked_turf = locate(x, y, t_center.z)
			if(checked_turf)
				turf_list += checked_turf

		y = t_center.y + c_dist - 1
		x = t_center.x + c_dist
		for(y in t_center.y - c_dist to y)
			checked_turf = locate(x, y, t_center.z)
			if(checked_turf)
				turf_list += checked_turf

		y = t_center.y - c_dist
		x = t_center.x + c_dist - 1
		for(x in t_center.x - c_dist to x)
			checked_turf = locate(x, y, t_center.z)
			if(checked_turf)
				turf_list += checked_turf

		y = t_center.y - c_dist + 1
		x = t_center.x - c_dist
		for(y in y to t_center.y + c_dist)
			checked_turf = locate(x, y, t_center.z)
			if(checked_turf)
				turf_list += checked_turf
		c_dist++
		if(tick_checked)
			CHECK_TICK

	return turf_list

/**
 * Checks whether the target turf is in a valid state to accept a directional construction
 * such as windows or railings.
 *
 * Returns FALSE if the target turf cannot accept a directional construction.
 * Returns TRUE otherwise.
 *
 * Arguments:
 * * dest_turf - The destination turf to check for existing directional constructions
 * * test_dir - The prospective dir of some atom you'd like to put on this turf.
 * * is_fulltile - Whether the thing you're attempting to move to this turf takes up the entire tile or whether it supports multiple movable atoms on its tile.
 */
/proc/valid_build_direction(turf/dest_turf, test_dir, is_fulltile = FALSE)
	if(!dest_turf)
		return FALSE
	for(var/obj/turf_content in dest_turf)
		if(turf_content.obj_flags & OBJ_BLOCKS_CONSTRUCTION_DIR)
			if(is_fulltile)  // for making it so fulltile things can't be built over directional things--a special case
				return FALSE
			if(turf_content.dir == test_dir)
				return FALSE
	return TRUE

/proc/iswall(turf/T)
	return (istype(T, /turf/simulated/wall) || istype(T, /turf/unsimulated/wall) || istype(T, /turf/simulated/shuttle/wall))

/proc/isfloor(turf/T)
	return (istype(T, /turf/simulated/floor) || istype(T, /turf/unsimulated/floor) || istype(T, /turf/simulated/shuttle/floor))

/proc/turf_clear(turf/T)
	for(var/atom/A in T)
		if(!(A.atom_flags & ATOM_ABSTRACT))
			return 0
	return 1

/**
 * Picks a turf without a mob from the given list of turfs, if one exists.
 * If no such turf exists, picks any random turf from the given list of turfs.
 */
/proc/pick_mobless_turf_if_exists(list/start_turfs)
	if(!start_turfs.len)
		return null

	var/list/available_turfs = list()
	for(var/start_turf in start_turfs)
		var/mob/M = locate() in start_turf
		if(!M)
			available_turfs += start_turf
	if(!available_turfs.len)
		available_turfs = start_turfs
	return pick(available_turfs)

/**
 * Picks a turf that is clearance tiles away from the map edge given by dir, on z-level Z.
 */
/proc/pick_random_edge_turf(dir, Z, clearance = TRANSITIONEDGE + 1)
	if(!dir)
		return
	switch(dir)
		if(NORTH)
			return locate(rand(clearance, world.maxx - clearance), world.maxy - clearance, Z)
		if(SOUTH)
			return locate(rand(clearance, world.maxx - clearance), clearance, Z)
		if(EAST)
			return locate(world.maxx - clearance, rand(clearance, world.maxy - clearance), Z)
		if(WEST)
			return locate(clearance, rand(clearance, world.maxy - clearance), Z)

/proc/is_below_sound_pressure(turf/T)
	var/pressure =  T.return_pressure() || 0
	if(pressure < SOUND_MINIMUM_PRESSURE)
		return TRUE
	return FALSE

/**
 *! Turf Manipulation
 */

/**
 * Returns an assoc list that describes how turfs would be changed if the
 * Turfs in turfs_src were translated by shifting the src_origin to the dst_origin
 */
/proc/get_turf_translation(turf/src_origin, turf/dst_origin, list/turfs_src)
	var/list/turf_map = list()
	for(var/turf/source in turfs_src)
		var/x_pos = (source.x - src_origin.x)
		var/y_pos = (source.y - src_origin.y)
		var/z_pos = (source.z - src_origin.z)

		var/turf/target = locate(dst_origin.x + x_pos, dst_origin.y + y_pos, dst_origin.z + z_pos)
		if(!target)
			log_debug(span_debug_error("Null turf in translation @ ([dst_origin.x + x_pos], [dst_origin.y + y_pos], [dst_origin.z + z_pos] [ADMIN_JMP(dst_origin)])"))
		// If target is null, preserve that information in the turf map.
		turf_map[source] = target

	return turf_map

/proc/translate_turfs(list/translation, area/base_area = null, turf/base_turf)
	for(var/turf/source in translation)

		var/turf/target = translation[source]

		if(target)
			if(base_area)
				ChangeArea(target, get_area(source))
			var/leave_turf = base_turf ? base_turf : /turf/simulated/floor/plating
			translate_turf(source, target, leave_turf)
			if(base_area)
				ChangeArea(source, base_area)

	// Change the old turfs (Currently done by translate_turf for us)
	// for(var/turf/source in translation)
	// 	source.ChangeTurf(base_turf ? base_turf : get_base_turf_by_area(source), 1, 1)

/proc/translate_turf(turf/Origin, turf/Destination, turftoleave = null)

	// You can stay, though.
	if (istype(Origin, /turf/space))
		log_debug(span_debug_error("Tried to translate a space turf: src=[log_info_line(Origin)][ADMIN_JMP(Origin)] dst=[log_info_line(Destination)][ADMIN_JMP(Destination)]"))
		return FALSE	// TODO - Is this really okay to do nothing?

	var/turf/X	// New Destination Turf

	var/old_dir1 = Origin.dir
	var/old_icon_state1 = Origin.icon_state
	var/old_icon1 = Origin.icon
	var/old_underlays = Origin.underlays.Copy()
	var/old_decals = Origin.decals ? Origin.decals.Copy() : null

	X = Destination.PlaceOnTop(Origin.type)
	X.setDir(old_dir1)
	X.icon_state = old_icon_state1
	X.icon = old_icon1
	X.copy_overlays(Origin, TRUE)
	X.underlays = old_underlays
	X.decals = old_decals

	/// Move the air from source to dest.
	var/turf/simulated/ST = Origin
	if (istype(ST))
		var/turf/simulated/SX = X
		if(!SX.air)
			SX.make_air()
		SX.air.copy_from(ST.copy_cell_volume())

	var/z_level_change = FALSE
	if (Origin.z != X.z)
		z_level_change = TRUE

	// Move the objects. Not forceMove because the object isn't "moving" really, it's supposed to be on the "same" turf.
	for(var/obj/O in Origin)
		if(O.atom_flags & ATOM_ABSTRACT)
			continue
		O.loc = X
		O.update_light()
		// The objects still need to know if their z-level changed.
		if (z_level_change)
			O.on_changed_z_level(Origin.z, X.z)

	// Move the mobs unless it's an AI eye or other eye type.
	for(var/mob/M in Origin)
		if (M.atom_flags & ATOM_ABSTRACT)
			continue
		if (isEye(M))
			// If we need to check for more mobs, I'll add a variable.
			continue
		M.loc = X

		// Same goes for mobs.
		if (z_level_change)
			M.on_changed_z_level(Origin.z, X.z)

	if (turftoleave)
		Origin.ChangeTurf(turftoleave)
	else
		Origin.ScrapeAway()

	return TRUE

/**
 * Used for border objects. This returns true if this atom is on the border between the two specified turfs.
 * This assumes that the atom is located inside the target turf.
 */
/atom/proc/is_between_turfs(turf/origin, turf/target)
	if (atom_flags & ATOM_BORDER)
		var/testdir = get_dir(target, origin)
		return (dir & testdir)
	return TRUE

/**
 * Checks whether the target turf is in a valid state to accept a directional window
 * or other directional pseudo-dense object such as railings.
 *
 * Returns FALSE if the target turf cannot accept a directional window or railing.
 * Returns TRUE otherwise.
 *
 * Arguments:
 * * dest_turf - The destination turf to check for existing windows and railings
 * * test_dir - The prospective dir of some atom you'd like to put on this turf.
 * * is_fulltile - Whether the thing you're attempting to move to this turf takes up the entire tile or whether it supports multiple movable atoms on its tile.
 */
/proc/valid_window_location(turf/dest_turf, test_dir, is_fulltile = FALSE)
	if(!dest_turf)
		return FALSE
	for(var/obj/turf_content in dest_turf)
		if(istype(turf_content, /obj/machinery/door/window))
			if((turf_content.dir == test_dir) || is_fulltile)
				return FALSE
		if(istype(turf_content, /obj/structure/windoor_assembly))
			var/obj/structure/windoor_assembly/windoor_assembly = turf_content
			if(windoor_assembly.dir == test_dir || is_fulltile)
				return FALSE
		if(istype(turf_content, /obj/structure/window))
			var/obj/structure/window/window_structure = turf_content
			if(window_structure.dir == test_dir || window_structure.fulltile || is_fulltile)
				return FALSE
		if(istype(turf_content, /obj/structure/railing))
			var/obj/structure/railing/rail = turf_content
			if(rail.dir == test_dir || is_fulltile)
				return FALSE
	return TRUE
