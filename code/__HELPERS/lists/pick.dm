/**
 * Picks a random element from a list based on a weighting system:
 * - 1. Adds up the total of weights for each element.
 * - 2. Gets a number between 1 and that total.
 * - 3. For each element in the list, subtracts its weighting from that number.
 * - 4. If that makes the number 0 or less, return that element.
 */
/proc/pickweight(list/L)
	var/total = 0
	var/item
	for (item in L)
		if (!L[item])
			L[item] = 1
		total += L[item]

	total = rand(1, total)
	for (item in L)
		total -=L [item]
		if (total <= 0)
			return item

	return null

/**
 * The original pickweight proc will sometimes pick entries with zero weight.
 * I'm not sure if changing the original will break anything, so I left it be.
 */
/proc/pickweightAllowZero(list/L)
	var/total = 0
	var/item
	for (item in L)
		if (!L[item])
			L[item] = 0
		total += L[item]

	total = rand(0, total)
	for (item in L)
		total -=L [item]
		if (total <= 0 && L[item])
			return item

	return null

/**
 * Pick a random element from the list and remove it from the list.
 */
/proc/pick_n_take(list/L)
	RETURN_TYPE(L[_].type)
	if(L.len)
		var/picked = rand(1,L.len)
		. = L[picked]
		// Cut is far more efficient that Remove()
		L.Cut(picked,picked+1)
