//Generates assets based on iconstates of a single icon
/datum/asset_pack/simple/icon_states
	abstract_type = /datum/asset_pack/simple/icon_states
	var/icon
	var/list/directions = list(SOUTH)
	var/frame = 1
	var/movement_states = FALSE

	var/prefix = "default" //asset_name = "[prefix].[icon_state_name].png"
	var/generic_icon_names = FALSE //generate icon filenames using generate_asset_name() instead the above format

/datum/asset_pack/simple/icon_states/register(_icon = icon)
	for(var/icon_state_name in icon_states(_icon))
		for(var/direction in directions)
			var/asset = icon(_icon, icon_state_name, direction, frame, movement_states)
			if (!asset)
				continue
			asset = fcopy_rsc(asset) //dedupe
			var/prefix2 = (directions.len > 1) ? "[dir2text(direction)]." : ""
			var/asset_name = SANITIZE_FILENAME("[prefix].[prefix2][icon_state_name].png")
			if (generic_icon_names)
				asset_name = "[generate_asset_name(asset)].png"

			SSassets.transport.register_asset(asset_name, asset)

/datum/asset_pack/simple/icon_states/multiple_icons
	abstract_type = /datum/asset_pack/simple/icon_states/multiple_icons
	var/list/icons

/datum/asset_pack/simple/icon_states/multiple_icons/register()
	for(var/i in icons)
		..(i)
