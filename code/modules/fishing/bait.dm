/obj/item/bait_can
	abstract_type = /obj/item/bait_can
	name = "can o bait"
	desc = "there's a lot of them in there, getting them out takes a while though"
	icon = 'icons/modules/fishing/storage.dmi'
	icon_state = "bait_can"
	w_class = WEIGHT_CLASS_SMALL
	/// Tracking until we can take out another bait item
	COOLDOWN_DECLARE(bait_removal_cooldown)
	/// What bait item it produces
	var/bait_type
	/// Time between bait retrievals
	var/cooldown_time = 10 SECONDS
	/// how much it has left; null for infinite
	var/bait_left = 50

/obj/item/bait_can/attack_self(mob/user, modifiers)
	. = ..()
	var/fresh_bait = retrieve_bait(user)
	if(fresh_bait)
		user.put_in_hands(fresh_bait)

/obj/item/bait_can/proc/retrieve_bait(mob/user)
	if(!isnull(bait_left) && (bait_left <= 0))
		user.bubble_action_feedback("it's empty", src)
		return
	if(!COOLDOWN_FINISHED(src, bait_removal_cooldown))
		user.bubble_action_feedback("wait a bit", src)
		return
	if(!isnull(bait_left))
		--bait_left
	COOLDOWN_START(src, bait_removal_cooldown, cooldown_time)
	return new bait_type(src)

/obj/item/bait_can/worm
	name = "can o' worm"
	desc = "A can of worms. Useful for fishing."
	bait_type = /obj/item/food/bait/worm

/obj/item/bait_can/worm/premium
	name = "can o' worm deluxe"
	desc = "A can of fancy worms. Useful for fishing, even more so."
	bait_type = /obj/item/food/bait/worm/premium

#warn below

/obj/item/food/bait
	abstract_type = /obj/item/food/bait
	name = "this is bait"
	desc = "you got baited."
	icon = 'icons/modules/fishing/bait.dmi'
	/// Quality trait of this bait
	var/bait_quality = BASIC_QUALITY_BAIT_TRAIT
	/// Icon state added to main fishing rod icon when this bait is equipped
	var/rod_overlay_icon_state

/obj/item/food/bait/Initialize(mapload)
	. = ..()
	ADD_TRAIT(src, bait_quality, INNATE_TRAIT)

/obj/item/food/bait/worm
	name = "worm"
	desc = "It's a wriggling worm from a can of fishing bait. You're not going to eat it are you ?"
	icon_state = "worm"
	food_reagents = list(/datum/reagent/consumable/nutriment/protein = 1)
	tastes = list("meat" = 1, "worms" = 1)
	foodtypes = GROSS | MEAT | BUGS
	w_class = WEIGHT_CLASS_TINY
	bait_quality = BASIC_QUALITY_BAIT_TRAIT
	rod_overlay_icon_state = "worm_overlay"

/obj/item/food/bait/worm/premium
	name = "extra slimy worm"
	desc = "This worm looks very sophisticated."
	bait_quality = GOOD_QUALITY_BAIT_TRAIT

/obj/item/food/bait/doughball
	name = "doughball"
	desc = "Small piece of dough. Simple but effective fishing bait."
	icon = 'icons/obj/fishing.dmi'
	icon_state = "doughball"
	food_reagents = list(/datum/reagent/consumable/nutriment/protein = 1)
	tastes = list("dough" = 1)
	foodtypes = GRAIN
	w_class = WEIGHT_CLASS_TINY
	bait_quality = BASIC_QUALITY_BAIT_TRAIT
	rod_overlay_icon_state = "dough_overlay"

/// These are generated by tech fishing rod
/obj/item/food/bait/doughball/synthetic
	name = "synthetic doughball"
	icon_state = "doughball"
	preserved_food = TRUE
