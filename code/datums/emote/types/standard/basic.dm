//* This file is explicitly licensed under the MIT license. *//
//* Copyright (c) 2024 Citadel Station Developers           *//

/**
 * Basic emotes that just emit a simple message or something.
 *
 * * This is in reality just a generic emote subtype that handles most cases.
 * * 'arbitrary' will be a list of key-values, with EMOTE_PARAMETER_KEY_*.
 * * parameters are a list of strings; you can do strings with spaces with "string here". \" will escape doublequotes, \\ will escape a \
 * * named parameters are not yet supported. it's too expensive to process these.
 * * you are allowed one custom parameter in addition to default parameters generated by this type (like 'target').
 * * the custom parameter will be under EMOTE_PARAMETER_KEY_CUSTOM.
 *
 * Params
 */
/datum/emote/standard/basic
	abstract_type = /datum/emote/standard/basic

	//* Feedback *//

	/// outgoing saycode type flags
	///
	/// If set to AUTO, the following happens:
	/// * If the hearer can see, normal / visible text is used
	/// * If the hearer can't see an actor, 'someone / something' replaces that actor
	/// * If the hearer cannot see, audible is used with no replacement.
	///
	/// If set to anything that isn't AUTO, the following happens:
	/// * ALWAYS, LIVING, CONSCIOUS will always use visible string, never audible
	/// * VISIBLE will always use visible string, never audible
	/// * AUDIBLE will always use audible string, falling back to visible if it exists
	var/feedback_saycode_type = SAYCODE_TYPE_AUTO

	/**
	 * Override order as follows:
	 *
	 * 1. Miming, if miming
	 * 2. Muzzled, if muzzled
	 * 3. Targeted, if there is a target
	 * 4. Default
	 */

	/// if existing, and we're miming, we immediately switch saycode mode to visible if we're on auto
	///
	/// * %%USER%% is replaced with the user.
	/// * If the replacement is at the start of the string, it'll be capitalized as needed.
	/// * HTML is valid
	var/feedback_special_miming
	/// targeted version of miming
	///
	/// * %%USER%% is replaced with the user.
	/// * %%TARGET%% is replaced with a target.
	/// * If the replacement is at the start of the string, it'll be capitalized as needed.
	/// * HTML is valid
	var/feedback_special_miming_targeted

	/// if existing, and we're muzzled, we immediately switch to this
	///
	/// * %%USER%% is replaced with the user.
	/// * If the replacement is at the start of the string, it'll be capitalized as needed.
	/// * HTML is valid
	var/feedback_special_muzzled
	/// targeted version of muzzled
	///
	/// * %%USER%% is replaced with the user.
	/// * %%TARGET%% is replaced with a target.
	/// * If the replacement is at the start of the string, it'll be capitalized as needed.
	/// * HTML is valid
	var/feedback_special_muzzled_targeted
	/// audible descriptor for muzzled
	///
	/// * HTML is valid
	var/feedback_special_muzzled_audible

	/// the default feedback string
	///
	/// * If heard, audible has %%USER%% replaced with "someone" if the viewer is blind.
	/// * If the replacement is at the start of the string, it'll be capitalized as needed.
	/// * HTML is valid
	var/feedback_default = "<b>%%USER%%</b> does something that isn't implemented by the coders. (Yell at coders, someone forgot a default string.)"
	/// the default targeted feedback sting
	///
	/// * %%USER%% is replaced with the user.
	/// * %%TARGET%% is replaced with a target.
	/// * If the replacement is at the start of the string, it'll be capitalized as needed.
	/// * HTML is valid
	var/feedback_default_targeted
	/// the default audible descriptor
	///
	/// * HTML is valid
	var/feedback_default_audible = "You hear something that isn't implemented by the coders. (Yell at coders, someone forgot a default string.)"

	//* Parameter *//

	/// has a custom parameter
	/// * if enabled, it goes *before* target
	/// * this means that it must be specified to specify a target at all
	var/parameter_custom = FALSE
	/// your custom parameter help string
	var/parameter_custom_description

	//* Targeting *//

	/// Allows targeting?
	/// * overrides [target_required]
	var/target_allowed = FALSE
	/// requires a target?
	var/target_required = FALSE
	/// maximum distance target can be from us
	var/target_range = 7
	/// allow target self
	var/target_allow_self = FALSE

/datum/emote/standard/basic/New()
	if(isnull(parameter_description))
		parameter_description = generate_parameter_description()
	else
		stack_trace("[NAMEOF(src, parameter_description)] was set, but it should be autogenerated!")
	..()

/datum/emote/standard/basic/proc/generate_parameter_description()
	return "[parameter_custom ? "\[[(parameter_custom_description || "Custom Parameter (Yell at coders!)")]\]" : ""]\
	[target_allowed ? " \[target[target_required ? "" : "?"]\]" : ""]"

/datum/emote/standard/basic/process_parameters(parameter_string, datum/event_args/actor/actor, silent)
	var/list/tokenized = tokenize_parameters(parameter_string, actor, silent)
	if(tokenized == null)
		return null
	. = ..()
	if(parameter_custom)
		var/got_target
		if(target_allowed)
			if(target_required)
				if(length(tokenized) <= 2)
					loudly_reject_failure(parameter_string, actor, silent, "A target is required.")
					return null
			if(length(tokenized) >= 2)
				.[EMOTE_PARAMETER_KEY_TARGET] = tokenized[2]
				got_target = TRUE
		if(length(tokenized) >= 1)
			.[EMOTE_PARAMETER_KEY_CUSTOM_TOKENS] = tokenized.Copy(1, length(tokenized) + got_target ? 0 : 1)
	else
		if(target_allowed)
			if(target_required)
				if(length(tokenized) <= 1)
					loudly_reject_failure(parameter_string, actor, silent, "A target is required.")
					return null
			if(length(tokenized) >= 1)
				.[EMOTE_PARAMETER_KEY_TARGET] = tokenized[1]

/datum/emote/standard/basic/run_emote(datum/event_args/actor/actor, list/arbitrary, silent, used_binding)
	var/target_string = arbitrary[EMOTE_PARAMETER_KEY_TARGET]
	if(target_string)
		var/mob/resolved
		for(var/mob/maybe_target in hearers())
			if(maybe_target == actor.performer && !target_allow_self)
				continue
			if(findtext(maybe_target.name, target_string))
				resolved = maybe_target
				break
		if(!resolved)
			if(!silent)
				actor?.chat_feedback(SPAN_WARNING("No one around you's named '[target_string]'!"))
			return FALSE
		else
			arbitrary[EMOTE_PARAMETER_KEY_TARGET_RESOLVED] = resolved
	. = ..()
	if(!.)
		return
	on_run_emote(
		actor,
		arbitrary,
		arbitrary[EMOTE_PARAMETER_KEY_CUSTOM_TOKENS],
		arbitrary[EMOTE_PARAMETER_KEY_TARGET_RESOLVED],
	)

/datum/emote/standard/basic/proc/on_run_emote(datum/event_args/actor/actor, list/arbitrary, list/maybe_custom_param_tokens, atom/maybe_target)
	var/template_string_visible = get_template_string(actor, arbitrary, FALSE)
	var/template_string_audible = get_template_string(actor, arbitrary, TRUE)
	var/list/template_parameters = get_template_parameters(actor, arbitrary)

	var/out_visible = string_format(template_string_visible, template_parameters)
	var/out_audible = string_format(template_string_audible, template_parameters)

	var/list/mob/hearing_mobs = actor.performer.saycode_view_query(null, TRUE, FALSE)
	// TODO: centralized observer pref check in saycode_view_query
	var/optimize_this_later_max_number = world_view_max_number() + 2
	// TODO: proper runechat stuff.
	actor.performer.say_overhead(out_visible)
	for(var/mob/hearing as anything in hearing_mobs)
		if(isobserver(hearing))
			if((get_dist(hearing, src) > optimize_this_later_max_number) && !hearing.get_preference_toggle(/datum/game_preference_toggle/observer/ghost_sight))
				continue
		if(hearing.is_blind())
			if(hearing.is_deaf())
			else
				hearing.show_message(out_audible, SAYCODE_TYPE_ALWAYS)
		else
			hearing.show_message(out_visible, SAYCODE_TYPE_ALWAYS)

/**
 * Get template parameters for 'feedback_' vars.
 */
/datum/emote/standard/basic/proc/get_template_parameters(datum/event_args/actor/actor, list/arbitrary)
	. = list("USER" = actor.performer)
	if(target_allowed)
		var/atom/maybe_target = arbitrary[EMOTE_PARAMETER_KEY_TARGET_RESOLVED]
		.["TARGET"] = maybe_target ? "[maybe_target]" : "something"

/datum/emote/standard/basic/proc/get_template_string(datum/event_args/actor/actor, list/arbitrary, audible)
	// TODO: miming
	// TODO: muzzled
	if(audible)
		return feedback_default_audible
	if(target_allowed && arbitrary[EMOTE_PARAMETER_KEY_TARGET])
		return feedback_default_targeted
	return feedback_default

/**
 * Processes a custom parameter
 */
/datum/emote/standard/basic/proc/process_custom_parameter(datum/event_args/actor/actor, string)
	// default to just spitting the string back out
	return string
